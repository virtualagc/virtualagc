### FILE="Main.annotation"
## Copyright:	Public domain.
## Filename:	RADAR_TEST_PROGRAMS.agc
## Purpose:	A module for revision 0 of BURST120 (Sunburst).
##		It is part of the source code for the Lunar Module's (LM)
##		Apollo Guidance Computer (AGC) for Apollo 5.
## Assembler:	yaYUL
## Contact:	Ron Burkey <info@sandroid.org>.
## Website:	www.ibiblio.org/apollo/index.html
## Mod history:	2016-09-30 RSB	Created draft version.
##		2016-10-06 RSB	Completed transcription.
##		2016-12-04 RSB	Proofed comments using octopus/ProoferComments.
##		2016-12-05 RSB	Comment-proofing pass with octopus/ProoferComments completed;
##				changes made.

## Page 198
		BANK	10
		EBANK=	RSTKLOC
		
# RADAR SAMPLING LOOP.

RADSAMP		CCS	RSAMPDT		# TIMES NORMAL ONCE-PER-SECOND SAMPLING.
		TCF	+2
		
		TCF	TASKOVER	# +0 INSERTED MANUALLY TERMINATES TEST.
		
		TC	WAITLIST
		EBANK=	RSTKLOC
		2CADR	RADSAMP
		
		CAF	PRIO25
		TC	NOVAC
		EBANK=	RSTKLOC
		2CADR	DORSAMP
		
		CAF	1/6		# FOR CYCLIC SAMPLING, RTSTDEX =
		EXTEND			# RTSTLOC/6 + RTSTBASE.
		MP	RTSTLOC
		AD	RTSTBASE	# 0 FOR RR, 2 FOR LR.
		TS	RTSTDEX
		
		TCF	TASKOVER
		
# DO THE ACTUAL RADAR SAMPLE.

DORSAMP		TC	VARADAR		# SELECTS VARIABLE RADAR CHANNEL.
		TC	BANKCALL
		CADR	RADSTALL
		INCR	RFAILCNT	# ADVANCE FAIL COUNTER BUT ACCEPT BAD DATA
		
DORSAMP2	INHINT			# YES - UPDATE TM BUFFER.
		DXCH	SAMPLSUM
		INDEX	RTSTLOC
		DXCH	RSTACK
		
		DXCH	OPTYHOLD
		INDEX	RSTKLOC
		DXCH	RSTACK	+2
		
		DXCH	TIMEHOLD
		INDEX	RSTKLOC
		DXCH	RSTACK	+4
		
		CS	RTSTLOC		# CYCLE RTSTLOC.
		AD	RTSTMAX
		EXTEND
		
## Page 199		
		BZF	+3
		CA	RTSTLOC
		AD	SIX
		TS	RTSTLOC
		
		CCS	RSAMPDT		# SEE IF TIME TO RE-SAMPLE.
		TCF	ENDOFJOB	# NO - WAIT FOR T3 (REGULAR SAMPLING).
		
		TCF	ENDOFJOB	# TEST TERMINATED.
		TCF	DORSAMP		# JUMP RIGHT BACK AND GET ANOTHER SAMPLE.
		
1/6		DEC	.17
		
# VARIABLE RADAR DATA CALLER FOR ONE MEASUREMENT ONLY.

VARADAR		CAF	ONE		# WILL BE SENT TO RADAR ROUTINE IN A BY
		TS	BUF2		# SWCALL.
		INDEX	RTSTDEX
		CAF	RDRLOCS
		TCF	SWCALL		# NOT TOUCHING Q.
		
RDRLOCS		CADR	RRRANGE		# = 0
		CADR	RRRDOT		# = 1
		CADR	LRVELX		# = 2
		CADR	LRVELY		# = 3
		CADR	LRVELZ		# = 4
		CADR	LRALT		# = 5
		

