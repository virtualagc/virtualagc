00000100/********************************************************************/
00000200/*------------------------------------------------------------------*/
00000300/*                                                                  */
00000400/*   NAME:     TRANSFER                                             */
00000500/*                                                                  */
00000600/*------------------------------------------------------------------*/
00000700/*                                                                  */
00000800/*   FUNCTION: TO TRANSFER A FILE BETWEEN THE MAC AND CIN.          */
00000900/*                                                                  */
00001000/*------------------------------------------------------------------*/
00001100/*                                                                  */
00001200/*   SYNTAX :                                                       */
00001300/*     TRANSFER LEVEL(&SYSUID.) TRACE                               */
00001400/*                                                                  */
00001500/*     ALIAS    - NONE.                                             */
00001600/*     REQUIRED - NONE.                                             */
00001800/*                                                                  */
00001900/*------------------------------------------------------------------*/
00002000/*                                                                  */
00002100/*   INVOKES:  PDSALL CLIST                                         */
00002200/*                                                                  */
00002300/*------------------------------------------------------------------*/
00002400/*                                                                  */
00002500/*   PARAMETERS:                                                    */
00002600/*                                                                  */
00002700/*     LEVEL    DEFAULT: &SYSUID.                                   */
00002800/*              THE LEVEL WHERE THE FILE RESIDES.                   */
00002900/*                                                                  */
00003000/*     TRACE    OPTIONAL                                            */
00003100/*              EXECUTION OF THE CLIST WILL BE DISPLAYED AT THE     */
00003200/*              TERMINAL                                            */
00003300/*                                                                  */
00003400/*------------------------------------------------------------------*/
00003500/*                                                                  */
00003600/*   INPUT DATA SET NAMES:                                          */
00003700/*                                                                  */
00003800/*     &SYSUID.TRANSFER.MEMLIST                                     */
00003900/*         MEMBER LIST GENERATED BY PDSALL                          */
00004000/*                                                                  */
00004100/*------------------------------------------------------------------*/
00004200/*                                                                  */
00004300/*   OUTPUT DATA SET NAMES:                                         */
00004400/*                                                                  */
00004500/*     NONE.                                                        */
00004600/*                                                                  */
00004700/*------------------------------------------------------------------*/
00004800/*                                                                  */
00004900/*   TEMPORARY DATA SET NAMES:                                      */
00005000/*                                                                  */
00005100/*     NONE.                                                        */
00005200/*                                                                  */
00005300/*------------------------------------------------------------------*/
00005400/*                                                                  */
00005500/*   COMMENTS:                                                      */
00005600/*                                                                  */
00005700/*     NONE.                                                        */
00005800/*                                                                  */
00005900/*------------------------------------------------------------------*/
00006000/*                                                                  */
00006100/*   REVISION HISTORY:                                              */
00006200/*                                                                  */
00006300/*     DATE      PCR#  NAME  DESCRIPTION                            */
00006400/*     --------  ----  ----  -------------------------------------  */
00006500/*     08/24/90  119   TEV   ORIGINAL CLIST                         */
00006600/*                                                                  */
00006700/*------------------------------------------------------------------*/
00006800/********************************************************************/
00006900 
00007000PROC   0   LEVEL(&SYSUID.) +
00007100           TRACE
00007200 
00007300GLOBAL RETCODE
00007400 
00007500IF &TRACE. NE THEN +
00007600   CONTROL LIST CONLIST
00007700 
00007800 /*********************************************/
00007900 /*    WRITE OUT SOME MESSAGES TO THE USER    */
00008000 /*    PRIOR TO ASKING FOR NECESSARY INFO.    */
00008100 /*********************************************/
00008200 
00008300WRITE IN ORDER TO TRANSFER, YOU MUST DO THE FOLLOWING BEFORE ANY
00008400WRITE  TRANSFERING CAN BEGIN:
00008500WRITE
00008600WRITE  1) GO TO THE FILE MENU AND SELECT SEND OR RECEIVE. SELECT
00008700WRITE      FORTENET TRANFER METHOD AND CHOOSE A MAC DIRECTORY.
00008800WRITE      UNDER THE CONFIGURE OPTION, SELECT NOKEY.
00008900WRITE  2) IN ORDER FOR THE NOKEY TO TAKE EFFECT, YOU MUST EXECUTE
00009000WRITE      A DUMMY TRANSFER. TO DO THIS, I RECOMMEND THE FOLLOWING
00009100WRITE      STEPS:
00009200WRITE        A) IF SENDING, PLACE A 'X' IN THE HOST FILE NAME FIELD,
00009300WRITE            CHOOSE ANY FILE (NOTHING WILL GET REALLY SENT SO
00009400WRITE            NOTHING WILL GET ERASED ON CIN) AND PRESS RETURN.
00009500WRITE        B) IF RECEIVING, PLACE A 'X' IN BOTH THE HOST FILE NAME
00009600WRITE            AND MACINTOSH FILE NAME FIELDS, CHOOSE ANY FILE
00009700WRITE            AND PRESS RETURN (NOTHING WILL BE RECEIVED).
00009800WRITE        C) IN BOTH INSTANCES, A SCREEN ERROR WILL OCCUR AND
00009900WRITE            YOU WILL BE RETURNED TO PANEL 6.
00010000WRITE
00010100WRITENR ARE THE ABOVE TWO CONDITIONS MET (CAN TRANSFER BEGIN)? (Y/N):
00010200 
00010300READ &REPLY
00010400IF &REPLY. NE &STR(Y) THEN +
00010500   EXIT
00010600 
00010700 /*******************************************/
00010800 /*    CHECK IF A FILENAME WAS SPECIFIED    */
00010900 /*******************************************/
00011000 
00011100TOP: WRITE
00011200     WRITE
00011300     WRITE WHAT IS THE NAME OF THE FILE TO TRANSFER?
00011400     WRITENR (RETURN EXITS) :
00011500     READ &FILENAME
00011600 
00011700IF &FILENAME. EQ  THEN EXIT
00011800 
00011900 /**************************************************/
00012000 /*    CHECK IF A VALID DIRECTION WAS SPECIFIED    */
00012100 /**************************************************/
00012200 
00012300DIR: WRITENR WHAT IS THE DIRECTION OF THE TRANSFER +
00012400             ((S)END OR (R)ECEIVE)?
00012500READ &DRECTION
00012600 
00012700IF &DRECTION. EQ &STR(S) OR &DRECTION. EQ &STR(R) THEN GOTO GETORG ELSE
00012800   GOTO DIR
00012900 
00013000 /*************************************************************/
00013100 /*    CHECK IF A VALID DATASET ORGANIZATION WAS SPECIFIED    */
00013200 /*************************************************************/
00013300 
00013400GETORG: WRITENR WHAT IS THE DATASET ORGANIZATION? (PDS OR SEQ)?
00013500READ &DSORG
00013600IF &DSORG. EQ &STR(PDS) OR &DSORG. EQ &STR(SEQ) THEN GOTO SETLIB ELSE
00013700   GOTO GETORG
00013800 
00013900 /**************************************/
00014000 /*    CHECK IF THE FILENAME EXISTS    */
00014100 /**************************************/
00014200 
00014300SETLIB: SET &LIB = &STR(&LEVEL..&FILENAME.)
00014400 
00014500IF &SYSDSN('&LIB.') NE OK THEN DO
00014600   WRITE ATTEMPTING TO TRANSFER TO/FROM NON-EXISTANT FILE!
00014700   EXIT
00014800END
00014900 
00015000 /************************************/
00015100 /*    ON ERROR, DO THE FOLLOWING    */
00015200 /************************************/
00015300 
00015400ERROR DO
00015500      IF &DSORG. EQ &STR(PDS) THEN DO
00015600         CLOSFILE MEMLIST
00015700         FREE F(MEMLIST)
00015800      END
00015900      EXIT
00016000END
00016100 
00016200 /****************************************/
00016300 /*    ON ATTENTION, DO THE FOLLOWING    */
00016400 /****************************************/
00016500 
00016600ATTN DO
00016700     IF &DSORG. EQ &STR(PDS) THEN DO
00016800        CLOSFILE MEMLIST
00016900        FREE F(MEMLIST)
00017000     END
00017100     EXIT
00017200END
00017300 
00017400 /*****************************************************/
00017500 /*    EXECUTE THE TRANSFER FOR A SEQUENTIAL FILE     */
00017600 /*****************************************************/
00017700 
00017800IF &DSORG. EQ &STR(SEQ) THEN DO
00017900   MAC: WRITENR MACINTOSH FILENAME?
00018000       READ &MACNAME
00018100   IF &MACNAME. EQ   THEN GOTO MAC
00018200 
00018300   IF &DRECTION. EQ &STR(S) THEN +
00018400      PCXFER DSN('&LIB.') FROM PCFILE(&MACNAME.) NOKEY
00018500   ELSE +
00018600      PCXFER DSN('&LIB.') TO   PCFILE(&MACNAME.) NOKEY
00018700END
00018800ELSE DO
00018900 
00019000 /**************************************************/
00019100 /*    MAKE A MEMBER LIST AND EXECUTE THE          */
00019200 /*    THE TRANSFER FOR EVERY MEMBER OF THE PDS    */
00019300 /**************************************************/
00019400 
00019500   PDSALL &LIB. LIB(&SYSUID..TRANSFER) MAKELIST
00019600 
00019700   ALLOC F(MEMLIST) DA('&SYSUID..TRANSFER.MEMLIST') OLD
00019800   OPENFILE MEMLIST INPUT
00019900 
00020000   DO WHILE &LASTCC. NE 400
00020100      GETFILE MEMLIST
00020200      SET &MEMLEN = 1
00020300      DO WHILE &SUBSTR(&MEMLEN.:&MEMLEN.,&MEMLIST) NE
00020400         SET &MEMLEN = &MEMLEN + 1
00020500      END
00020600      SET &MEMLEN = &MEMLEN - 1
00020700      SET &MEMLIST = &SUBSTR(1:&MEMLEN.,&MEMLIST)
00020800 
00020900      IF &DRECTION. EQ &STR(S) THEN +
00021000         PCXFER DSN('&LIB.(&MEMLIST.)') FROM PCFILE(&MEMLIST.) NOKEY
00021100      ELSE +
00021200         PCXFER DSN('&LIB.(&MEMLIST.)') TO   PCFILE(&MEMLIST.) NOKEY
00021300   END
00021400END
00021500GOTO TOP
