00000100/********************************************************************/
00000200/*------------------------------------------------------------------*/
00000300/*                                                                  */
00000400/*   NAME:     BYTECOMP                                             */
00000500/*                                                                  */
00000600/*------------------------------------------------------------------*/
00000700/*                                                                  */
00000800/*   FUNCTION: THIS ROUITNE IS THE CLIST INTERFACE ROUTINE FOR THE  */
00000900/*             UTILITY BY THE SAME NAME.  THE BYTECOMP UTILITY      */
00001000/*             (CLIST AND LOAD MODULE) WILL COMPARE TWO OBJECT      */
00001100/*             MODULES, GENERATED BY EITHER THE PASS OR BFS         */
00001200/*             COMPILERS, AND INDICATE WHETHER OR NOT THERE ARE ANY */
00001300/*             DIFFERENCES BETWEEN THEM.  THE COMPARE ROUTINE       */
00001400/*             ALLOWS FOR THE EXISTENCE OF DATE AND TIME STAMPS IN  */
00001500/*             THE OBJECT CODE AND ALLOWS FOR THE EXISTENCE OF      */
00001600/*             COMPILER VERSION NUMBERS.                            */
00001700/*                                                                  */
00001800/*------------------------------------------------------------------*/
00001900/*                                                                  */
00002000/*   SYNTAX :                                                       */
00002100/*     BYTECOMP 'NAME1' NAME2(&NAME1.) ID(&SYSUID.) LIB(&ID..TEST)  */
00002200/*              IN1(&LIB..OBJ(&NAME1.)) IN2(&LIB..OBJ(&NAME2.))     */
00002300/*              NOUTPUT REFRESH DELOBJ GENLIST TRACE                */
00002400/*                                                                  */
00002500/*     ALIAS    - NONE.                                             */
00002600/*     REQUIRED - NONE.                                             */
00002900/*                                                                  */
00003000/*------------------------------------------------------------------*/
00003100/*                                                                  */
00003200/*   INVOKES:  NCAMCM.TOOLS.HALLOAD(BYTECOMP)                       */
00003300/*                                                                  */
00003400/*------------------------------------------------------------------*/
00003500/*                                                                  */
00003600/*   PARAMETERS:                                                    */
00003700/*                                                                  */
00003800/*     NAME1    REQUIRED                                            */
00003900/*              NAME1 IS THE MEMBER NAME OF THE OBJECT MODULE TO BE */
00004000/*              COMPARED.                                           */
00004100/*                                                                  */
00004200/*     NAME2    DEFAULT: &NAME1                                     */
00004300/*              NAME2 IS THE MEMBER NAME OF THE OBJECT MODULE TO BE */
00004400/*              COMPARED AGAINST.  IF THIS PARM IS NOT USED THE     */
00004500/*              CLIST ASSUMES THE OBJECT MODULE TO BE COMPARED ARE  */
00004600/*              IN SEPARATE DATA SETS.                              */
00004700/*                                                                  */
00004800/*     ID       DEFAULT: &SYSUID.                                   */
00004900/*              THE USERS HIGH LEVEL QUALIFIER. NOT GENERALLY       */
00005000/*              NEEDED.                                             */
00005100/*                                                                  */
00005200/*     LIB      DEFAULT: &ID..TEST                                  */
00005300/*              IF THE FILES TO BE COMPARED ARE NOT IN DATA SETS    */
00005400/*              WITH THE FORM <HIGH LEVEL QUAL>.TEST.OBJ(..) THEN   */
00005500/*              LIB SHOULD BE SPECIFIED. IF SPECIFIED THE HLQ AND   */
00005600/*              TEST QUALIFIERS WILL BE REPLACED.  THE LIB SHOULD   */
00005700/*              ALWAYS INCLUDE THE HIGH LEVEL QUALIFIER.            */
00005800/*                                                                  */
00005900/*     IN1      DEFAULT: &LIB..OBJ(&NAME1.)                         */
00006000/*              THIS IS THE COMPLETE NAME OF THE FIRST DATA SET TO  */
00006100/*              BE COMPARED. THIS PARM SHOULD ONLY BE SUPPLIED WHEN */
00006200/*              THE OBJECT MODULES TO BE COMPARED ARE NOT IN DATA   */
00006300/*              SETS WITH EXPECTED FORMATS.                         */
00006400/*                                                                  */
00006500/*     IN2      DEFAULT: &LIB..OBJ(&NAME2.)                         */
00006600/*              SECOND OBJECT TO BE COMPARED. (SEE IN1)             */
00006700/*                                                                  */
00006800/*     SYSTEM   DEFUALT: PASS                                       */
00006900/*              PASS OR BFS, FOR VERSION SEPARATION PURPOSES        */
00007000/*                                                                  */
00007100/*     NOUTPUT  OPTIONAL                                            */
00007200/*              IF USED THEN ALL SCREEN DIRECTED OUTPUT IS SENT TO A*/
00007300/*              NULL UNIT.                                          */
00007400/*                                                                  */
00007500/*     REFRESH  OPTIONAL                                            */
00007600/*              USED WHEN A USER INVOKES THIS CLIST DIRECTLY TO     */
00007700/*              DELETE AND REALLOCATE THE REPORT FILE AND THE       */
00007800/*              COMPFC MEMBER LIST FILE.  WE ARE STARTING OVER.     */
00007900/*                                                                  */
00008000/*     DELOBJ   OPTIONAL                                            */
00008100/*              NORMALLY USED WHEN CALLED FROM ANOTHER CLIST {FOR   */
00008200/*              AUTOMATED TESTING}.  SPECIFIES THAT THE OBJECT FILES*/
00008300/*              COMPARED SHOULD BE DELETED AT THE CONCLUSION OF THE */
00008400/*              COMPARE IF THE COMPARE WAS GOOD.                    */
00008500/*                                                                  */
00008600/*     GENLIST  OPTIONAL                                            */
00008700/*              NORMALLY USED WHEN CALLED FROM ANOTHER CLIST {FOR   */
00008800/*              AUTOMATED TESTING}.  SPECIFIES THAT THE NAME OF THE */
00008900/*              OBJECT MODULE BEING TESTED SHOULD BE ADDED TO THE   */
00009000/*              MEMBER LIST IF IT FAILS THE TEST.                   */
00009100/*                                                                  */
00009200/*     TRACE    OPTIONAL                                            */
00009300/*              STANDARD TRACE FLAG                                 */
00009400/*                                                                  */
00009500/*------------------------------------------------------------------*/
00009600/*                                                                  */
00009700/*   INPUT DATA SET NAMES:                                          */
00009800/*                                                                  */
00009900/*   <ID>.TEST.OLD.OBJ(<NAME1>)                                     */
00010000/*     THIS IS THE FORM OF THE DATA SET NAME FOR THE FIRST OBJECT   */
00010100/*     TO BE COMPARED WHEN NAME1 AND NAME 2 ARE THE SAME.           */
00010200/*     NOTE1: IF THE INPUT PARAMETER NAME1 IS USED AND NAME2 IS NOT */
00010300/*     THE CLIST ASSUMES THAT THE TWO OBJECTS ARE CONTAINED IN      */
00010400/*     SEPARATE DATA SETS.  IF NAME2 IS SPECIFIED THEN THE CLIST    */
00010500/*     ASSUMES THAT THE OBJECTS ARE IN THE SAME DATA SET.  IN THIS  */
00010600/*     CASE THE DSN FORMAT WOULD BE : <ID>.TEST.OBJ(<NAMEX>) WHERE  */
00010700/*     X IS 1 OR 2                                                  */
00010800/*     NOTE2: <LIB> CAN BE PROVIDED TO REPLACE THE <ID>.TEST        */
00010900/*     IN EITHER OF THE ABOVE NAME FORMATS. <LIB> MUST CONTAIN A    */
00011000/*     HIGH LEVEL QUALIFIER.                                        */
00011100/*                                                                  */
00011200/*   <ID>.TEST.NEW.OBJ(<NAME2>)                                     */
00011300/*     DSN FORMAT FOR SECOND OBJECT.  NOTES 1 AND 2 ABOVE APPLY.    */
00011400/*                                                                  */
00011500/*------------------------------------------------------------------*/
00011600/*                                                                  */
00011700/*   OUTPUT DATA SET NAMES:                                         */
00011800/*                                                                  */
00011900/*   <LIB>.BYTCMP.RPT                                               */
00012000/*     THIS IS THE NAME FORMAT FOR THE PHYSICAL SEQUENTIAL DATA SET */
00012100/*     CONTAINING THE REPORT OF THE RESULTS OF THE COMPARE.  THIS   */
00012200/*     IS A CUMULATIVE RECORD OF COMPARES AND IS CLEARED OUT ONLY   */
00012300/*     WHEN THE USER SPECIFIES REFRESH.  IT IS ALLOCATED AUTOMAT-   */
00012400/*     ICALLY IF THE FILE DOES NOT EXIST.                           */
00012500/*                                                                  */
00012600/*   <LIB>.COMPFC.MLIST                                             */
00012700/*     THIS DATA SET IS ALSO A PHYSICAL SEQUENTIAL DATA SET BUT IS  */
00012800/*     USUALLY ONLY USED WHEN THE CLIST IS INVOKED BY ANOTHER CLIST.*/
00012900/*     THIS FILE CONTAINS A LIST OF THE MEMBERS THAT DID NOT HAVE   */
00013000/*     A SUCCESSFUL COMPARE (IE NAME1 OBJECT WAS DIFFERENT FROM     */
00013100/*     NAME2 OBJECT CODE).   IT IS ALLOCATED AUTOMATICALLY IF THE   */
00013200/*     FILE DOES NOT EXIST.                                         */
00013300/*                                                                  */
00013400/*------------------------------------------------------------------*/
00013500/*                                                                  */
00013600/*   TEMPORARY DATA SET NAMES:                                      */
00013700/*                                                                  */
00013800/*     <NAME1>.TEMPLIST                                             */
00013900/*     TEMPORARY DATASET USED TO PASS A COMPLETION CODE FROM THE    */
00014000/*     PROGRAM BYTECOMP TO THE CLIST.  THE HIGH LEVEL QUALIFIER IS  */
00014100/*     ALLOWED TO DEFAULT TO <SYSUID>.                              */
00014200/*                                                                  */
00014300/*------------------------------------------------------------------*/
00014400/*                                                                  */
00014500/*   COMMENTS:                                                      */
00014600/*     THIS CLIST IS INTENDED TO BE USED BY EITHER ANOTHER CLIST OR */
00014700/*     DIRECTLY BY A USER.  WHEN THIS CLIST IS CALLED BY ANOTHER    */
00014800/*     CLIST THEN THE FOLLOWING FORMAT FOR THE CALL IS RECOMMENDED: */
00014900/*                                                                  */
00015000/*       BYTECOMP NAME {LIB(LIB NAME)} NOUTPUT {DELOBJ} {GENLIST}   */
00015100/*                                                                  */
00015200/*     IT IS STRONGLY RECOMMENDED THAT THE NOUTPUT OPTION BE USED   */
00015300/*     WHEN CALLING THIS CLIST FROM ANOTHER CLIST TO SUPPRESS ANY   */
00015400/*     EXTRANEOUS OUTPUT.  THE OPTIONS ENCLOSED IN {} ARE CONSIDERED*/
00015500/*     OPTIONAL IN THIS CASE.                                       */
00015600/*                                                                  */
00015700/*     THE RECOMMENDED FORMAT TO USE WHEN A USER CALLS THIS CLIST   */
00015800/*     DIRECTLY IS :                                                */
00015900/*                                                                  */
00016000/*       BYTECOMP NAME {LIB(LIB NAME)} {REFRESH}                    */
00016100/*                                                                  */
00016200/*     IN THIS INSTANCE ONLY THE MEMBER NAME OF THE OBJECT TO BE    */
00016300/*     COMPARED IS NEEDED.  THE CLIST ASSUMES THAT THE TWO OBJECT   */
00016400/*     FILES HAVE THE SAME NAME BUT APPEAR IN DIFFERENT DATA SETS   */
00016500/*     WITH NAMES OF THE FORM :                                     */
00016600/*                                                                  */
00016700/*       &ID..TEST.OLD.OBJ(&NAME1.) - FOR OLD OBJECT FILE           */
00016800/*       &ID..TEST.NEW.OBJ(&NAME1.) - FOR NEW OBJECT FILE           */
00016900/*                                                                  */
00017000/*     THE REFRESH OPTION, AS STATED EARLIER, CAUSES THE CLIST TO   */
00017100/*     DELETE AND REALLOCATE THE REPORT FILE AND THE COMPFC         */
00017200/*     MEMBER LIST FILE.  THE LIB OPTION WILL REPLACE THE           */
00017300/*        &ID..TEST   IN THE DSN FORMATS WITH WHATEVER IS           */
00017400/*     SPECIFIED WITHIN THE ()'S.  OTHER OPTIONS, SUCH AS IN1 AND   */
00017500/*     IN2, CAN BE USED WHEN A USER INVOCATION IS PERFORMED IF      */
00017600/*     NECESSARY.  OPTIONS OTHER THAN THE ONES LISTED ABOVE CAN BE  */
00017700/*     USED IN A USER INVOCATION BUT IT IS NOT RECOMMEND BECAUSE OF */
00017800/*     THE NUMBER OF PARAMETERS TO BE TYPED IN.  IF YOU MUST USE    */
00017900/*     THESE PARAMETERS IN A USER INVOCATION PLEASE BE SURE TO      */
00018000/*     REVIEW THE CLIST AND BE SURE OF THEIR EFFECTS.               */
00018100/*                                                                  */
00018200/*------------------------------------------------------------------*/
00018300/*                                                                  */
00018400/*   REVISION HISTORY:                                              */
00018500/*                                                                  */
00018600/*     DATE      PCR#  NAME  DESCRIPTION                            */
00018700/*     --------  ----  ----  -------------------------------------  */
00018800/*     10/27/89  064   DJB   CREATED AND PUT INTO CM                */
00018900/*                                                                  */
00019000/*     6/25/90   099   TEV   UPDATED HEADER. ADDED CODE THAT CHECKS */
00019100/*                           THE PREFIX OF THE OBJ FILE FOR A #P,   */
00019200/*                           #C OR $ IF THE BFS SYSTEM IS SPECIFIED.*/
00019300/*                                                                  */
00019400/*     11/15/90  133   SDM   MODIFIED OUTPUT MESSAGES TO USER,      */
00019500/*                           CHANGED PARAMETER INIT TO REFRESH,     */
00019600/*                           DELETED PARAMETER NEWRUN, AND FREED    */
00019700/*                           CHANNEL7 BEFORE EXITING.               */
00019800/*                                                                  */
00019900/*     02/13/91  148   RAH   CHANGED LOCATION OF LOAD MODULE.       */
00020000/*                                                                  */
00020100/*     09/13/91  186   LJK   RENAMED CM LOAD MODULE.                */
00020200/*                                                                  */
00020300/*     12/01/94  337   JCS   CHECK FOR EMPTY INPUT FILES            */
00020400/*                           RENAME BYTECOMP.RPT TO BYTCMP.RPT      */
00020500/*     07/28/95  435   LJK   RENAMED COMPFC.MEMLIST TO COMPFC.MLIST */
00020600/*------------------------------------------------------------------*/
00020700/********************************************************************/
00020800 
00020900PROC 1 NAME1                     +
00021000       NAME2(&NAME1.)            +
00021100       ID(&SYSUID.)              +
00021200       LIB(&ID..TEST)            +
00021300       IN1(&LIB..OBJ(&NAME1.))   +
00021400       IN2(&LIB..OBJ(&NAME2.))   +
00021500       SYSTEM(PASS)              +
00021600       NOUTPUT                   +
00021700       REFRESH                   +
00021800       DELOBJ                    +
00021900       GENLIST                   +
00022000       TRACE
00022100 
00022200IF &TRACE EQ TRACE THEN CONTROL CONLIST LIST
00022300CONTROL END(DONE)
00022400CONTROL NOMSG
00022500  FREE F(CHANNEL4 CHANNEL5 CHANNEL6 CHANNEL7 CHANNEL8)
00022600  FREE ATTR(FBA133)
00022700  ATTR FBA133 RECFM(F B A) LRECL(133) BLKSIZE(6517)
00022800  DEL &NAME1..TEMPLIST
00022900CONTROL MSG
00023000 
00023100IF &REFRESH. NE THEN DO
00023200   CONTROL NOMSG
00023300   ERROR OFF
00023400   FREE DA('&LIB..BYTCMP.RPT')
00023500   DELETE '&LIB..BYTCMP.RPT'
00023600   FREE DA('&LIB..COMPFC.MLIST')
00023700   DELETE '&LIB..COMPFC.MLIST'
00023800   CONTROL MSG
00023900DONE
00024000IF  &SYSDSN('&LIB..BYTCMP.RPT') NE OK THEN                     +
00024100   ALLOC DA('&LIB..BYTCMP.RPT') SPACE(1,1) TRACKS DSORG(PS)    +
00024200                                  RECFM(F B) LRECL(133) BLKSIZE(6517)
00024300IF  &SYSDSN('&LIB..COMPFC.MLIST') NE OK THEN                     +
00024400   ALLOC DA('&LIB..COMPFC.MLIST') SPACE(1,1) TRACKS DSORG(PS)    +
00024500                                  RECFM(F B) LRECL(80) BLKSIZE(6160)
00024600 
00024700IF &SYSTEM. EQ &STR(BFS) THEN   +
00024800   IF &NAME1. EQ &NAME2. THEN   DO
00024900      SET &CHCKDSN1  = &STR(&SYSDSN('&LIB..OLD.OBJ(#P&NAME1.)'))
00025000      IF  &CHCKDSN1  = &STR(OK) THEN   +
00025100          SET &NAME1 = &STR(#P&NAME1.)
00025200      ELSE   DO
00025300        SET &CHCKDSN1  = &STR(&SYSDSN('&LIB..OLD.OBJ(#C&NAME1.)'))
00025400        IF  &CHCKDSN1  = &STR(OK) THEN +
00025500            SET &NAME1 = &STR(#C&NAME1.)
00025600        ELSE    +
00025700            SET &NAME1 = &STR($&NAME1.)
00025800      DONE
00025900        SET &IN1  EQ  &STR(&LIB..OLD.OBJ(&NAME1.))
00026000        SET &IN2  EQ  &STR(&LIB..NEW.OBJ(&NAME2.))
00026100   DONE
00026200   ELSE   DO
00026300     SET &CHCKDSN1  = &STR(&SYSDSN('&LIB..OBJ(#P&NAME1.)'))
00026400     SET &CHCKDSN2  = &STR(&SYSDSN('&LIB..OBJ(#P&NAME2.)'))
00026500     IF  &CHCKDSN1  = &STR(OK) AND &CHCKDSN2 = &STR(OK) THEN  DO
00026600         SET &NAME1.= &STR(#P&NAME1.)
00026700         SET &NAME2 = &STR(#P&NAME2.)
00026800     DONE
00026900     ELSE   DO
00027000       SET &CHCKDSN1  = &STR(&SYSDSN('&LIB..OBJ(#C&NAME1.)'))
00027100       SET &CHCKDSN2  = &STR(&SYSDSN('&LIB..OBJ(#C&NAME2.)'))
00027200       IF  &CHCKDSN1  = &STR(OK) AND &CHCKDSN2 = &STR(OK) THEN DO
00027300           SET &NAME1 = &STR(#C&NAME1.)
00027400           SET &NAME2 = &STR(#C&NAME2.)
00027500       DONE
00027600       ELSE   DO
00027700           SET &NAME1 = &STR($&NAME1.)
00027800           SET &NAME2 = &STR($&NAME2.)
00027900       DONE
00028000     DONE
00028100   DONE
00028200ELSE +
00028300  IF &NAME1. = &NAME2. THEN  DO
00028400     SET &IN1 = &STR(&LIB..OLD.OBJ(&NAME1.))
00028500     SET &IN2 = &STR(&LIB..NEW.OBJ(&NAME2.))
00028600  DONE
00028700 
00028800GLOBAL SAVECC
00028900SET &SAVECC EQ 0
00029000/* IF AN ERROR OCCURS IN THIS PROCEDURE */
00029100/* THEN EXIT                            */
00029200ERROR DO
00029300  FREE F(CHANNEL4 CHANNEL5 CHANNEL6 CHANNEL7 CHANNEL8)
00029400  FREE ATTR(FBA133)
00029500  ATTR FBA133 RECFM(F B A) LRECL(133) BLKSIZE(2660)
00029600  ERROR OFF
00029700  DEL &NAME1..TEMPLIST
00029800  ERROR  EXIT
00029900  EXIT
00030000DONE
00030100 
00030200ALLOC F(CHANNEL4) DA('&IN1.') SHR
00030300ALLOC F(CHANNEL5) DA('&IN2.') SHR
00030400ALLOC F(CHANNEL7) DA(&NAME1..TEMPLIST) RELEASE USING(FBA133) NEW +
00030500  SP(5 0) TRACK
00030600 
00030700IF &NOUTPUT NE THEN DO
00030800   ALLOC F(CHANNEL6) DUMMY
00030900   ALLOC F(CHANNEL8) DUMMY
00031000   DONE
00031100ELSE DO
00031200   ALLOC F(CHANNEL6) DA(*)
00031300   ALLOC F(CHANNEL8) DA(*)
00031400DONE
00031500 
00031600CALL 'NCAMCM.TOOLS.HALLOAD(BYTECOMP)'
00031700 
00031800OPENFILE CHANNEL7 INPUT
00031900GETFILE CHANNEL7
00032000SET SAVECC EQ &CHANNEL7
00032100CLOSFILE CHANNEL7
00032200CONTROL NOMSG
00032300FREE F(CHANNEL4 CHANNEL5 CHANNEL6 CHANNEL7 CHANNEL8)
00032400FREE ATTR(FBA133)
00032500DEL &NAME1..TEMPLIST
00032600CONTROL MSG
00032700 
00032800ALLOC F(REPORT) DA('&LIB..BYTCMP.RPT') MOD
00032900OPENFILE REPORT OUTPUT
00033000IF &SAVECC = 1          0 THEN +
00033100  SET REPORT = &STR(   OBJECT CODES &IN1. AND &IN2. MATCH)
00033200ELSE
00033300IF &SAVECC = 1          2 THEN +
00033400  SET REPORT = &STR(***ERROR - &IN1. OR &IN2. IS EMPTY!)
00033500ELSE
00033600IF &SAVECC = 1          1 THEN +
00033700  DO
00033800  SET REPORT = &STR(***OBJECT CODES &IN1. AND &IN2. ARE DIFFERENT)
00033900  IF &GENLIST NE THEN DO
00034000     ALLOC F(MLIST) DA('&LIB..COMPFC.MLIST') MOD
00034100     OPENFILE MLIST OUTPUT
00034200     SET MLIST = &STR(&NAME1.)
00034300     PUTFILE MLIST
00034400     CLOSFILE MLIST
00034500     FREE F(MLIST)
00034600  DONE
00034700DONE
00034800PUTFILE REPORT
00034900CLOSFILE REPORT
00035000FREE F(REPORT)
00035100 
00035200IF &DELOBJ NE AND &SAVECC = 1          0 THEN DO
00035300   CONTROL NOMSG
00035400   ERROR OFF
00035500   FREE F(CHANNEL4 CHANNEL5)
00035600   DEL '&IN1.'
00035700   DEL '&IN2.'
00035800   DEL '&LIB..NEW.LST(&NAME1.)'
00035900   DEL '&LIB..OLD.LST(&NAME2.)'
00036000   CONTROL MSG
00037000DONE
