00010000/********************************************************************/
00020000/*------------------------------------------------------------------*/
00030000/*                                                                  */
00040000/*   NAME:     XPLUPDAT                                             */
00050000/*                                                                  */
00060000/*------------------------------------------------------------------*/
00070000/*                                                                  */
00080000/*   FUNCTION: TO MERGE PASS AND BFS COMPILERS BASED ON UPDATE      */
00090000/*             DECKS CREATED BY SUPERC PROGRAM                      */
00100000/*                                                                  */
00110000/*------------------------------------------------------------------*/
00120000/*                                                                  */
00130000/*   SYNTAX :                                                       */
00140000/*     XPLUPDAT  PASS NEWFILE(NCAMCM.BFS.CURRENT.<PASS>.PROCS)      */
00150000/*               OLDFILE(<SYSUID>.REL99V0.<PASS>.PROCS)             */
00160000/*               MRGEFILE(<SYSUID>.REL99V0.<PASS>.MERGE.PROCS)      */
00170000/*               LISTFILE(<SYSUID>.REL99V0.<PASS>.COMPARE)          */
00180000/*               UPDATEDK(<SYSUID>.REL99V0.<PASS>.UPDATE)           */
00190000/*               PROFILE(<SYSUID>.SUPERC.STMTS) LIST() MERGE TRACE  */
00200000/*                                                                  */
00210000/*     ALIAS    - NONE.                                             */
00220000/*     REQUIRED - PASS.                                             */
00290000/*                                                                  */
00300000/*------------------------------------------------------------------*/
00310000/*                                                                  */
00320000/*   INVOKES:  PDSALL CLIST                                         */
00330000/*             SUPERC CLIST                                         */
00340000/*            'NCAMCM.TOOLS.HALLOAD(XPLUPDAT)'                      */
00350000/*                                                                  */
00360000/*------------------------------------------------------------------*/
00370000/*                                                                  */
00380000/*   PARAMETERS:                                                    */
00390000/*                                                                  */
00400000/*     PASS     REQUIRED                                            */
00410000/*              THE PHASE OF THE COMPILER                           */
00420000/*                                                                  */
00430000/*     NEWFILE  DEFAULT: NCAMCM.BFS.CURRENT.<PASS>.PROCS            */
00440000/*              THE LOCATION OF PROCS FOR BFS COMPILER              */
00450000/*                                                                  */
00460000/*     OLDFILE  DEFAULT: <SYSUID>.REL99V0.<PASS>.PROCS              */
00470000/*              THE LOCATION OF PROCS FOR PASS COMPILER             */
00480000/*                                                                  */
00490000/*     MRGEFILE DEFAULT: <SYSUID>.REL99V0.<PASS>.MERGE.PROCS        */
00500000/*              THE LOCATION OF MERGE PROCS                         */
00510000/*                                                                  */
00520000/*     LISTFILE DEFAULT: <SYSUID>.REL99V0.<PASS>.COMPARE            */
00530000/*              THE LOCATION OF THE PDS THAT CONTAINS THE           */
00540000/*              DIFFERENCES FILE GENERATED BY SUPERC                */
00550000/*                                                                  */
00560000/*     UPDATEDK DEFAULT: <SYSUID>.REL99V0.<PASS>.UPDATE             */
00570000/*              THE LOCATION OF THE PDS THAT CONTAINS THE UPDATE    */
00580000/*              DECKS TO BE CREATED BY SUPERC                       */
00590000/*                                                                  */
00600000/*     PROFILE  DEFAULT: <SYSUID>.SUPERC.STMTS                      */
00610000/*              THE LOCATION OF THE STATEMENTS FILE USED BY SUPERC  */
00620000/*                                                                  */
00630000/*     LIST     DEFAULT: NULL                                       */
00640000/*              IF NULL, GENERATES MEMBER LIST. OTHERWISE, LIST     */
00650000/*              CONTAINS THE MEMBER LIST OF DATASET YOU WANT TO     */
00660000/*              COMPARE. (EX: NCAMxxx.XPLUPDAT.MEMLIST)             */
00670000/*                                                                  */
00680000/*     MERGE    OPTIONAL                                            */
00690000/*              ONLY MERGE THE COMPILERS {ASSUMES THAT THE UPDATE   */
00700000/*              DECKS ARE ALREADY CREATED}                          */
00710000/*                                                                  */
00720000/*     TRACE    OPTIONAL                                            */
00730000/*              TRACE THE EXECUTION ON USER SCREEN                  */
00740000/*                                                                  */
00750000/*------------------------------------------------------------------*/
00760000/*                                                                  */
00770000/*   INPUT DATA SET NAMES:                                          */
00780000/*                                                                  */
00790000/*     NCAMCM.BFS.CURRENT.<PASS>.PROCS                              */
00800000/*         THE LOCATION OF BFS PROCS                                */
00810000/*                                                                  */
00820000/*     <SYSUID>.REL99V0.<PASS>.PROCS                                */
00830000/*         THE LOCATION OF PASS PROCS                               */
00840000/*                                                                  */
00850000/*     <SYSUID>.SUPERC.STMTS                                        */
00860000/*         THE STATEMENTS NEEDED BY SUPERC THAT SPECIFIES WHAT      */
00870000/*         COLUMNS TO RUN COMPARISONS ON (SHOULD BE CMPCOLM 1:80)   */
00880000/*                                                                  */
00890000/*------------------------------------------------------------------*/
00900000/*                                                                  */
00910000/*   OUTPUT DATA SET NAMES:                                         */
00920000/*                                                                  */
00930000/*     <SYSUID>.REL99V0.<PASS>.MERGE.PROCS                          */
00940000/*         THE LOCATION OF MERGE PROCS                              */
00950000/*                                                                  */
00960000/*     <SYSUID>.REL99V0.<PASS>.COMPARE                              */
00970000/*         THE CREATED DIFFERENCES FILE                             */
00980000/*                                                                  */
00990000/*     <SYSUID>.REL99V0.<PASS>.UPDATE                               */
01000000/*         THE CREATED UPDATE DECKS                                 */
01010000/*                                                                  */
01020000/*     <SYSUID>.XPLUPDAT.MEMLIST                                    */
01030000/*         THE MEMBER LIST CREATED BY THE PROGRAM (IF LIST IS NULL) */
01040000/*                                                                  */
01050000/*------------------------------------------------------------------*/
01060000/*                                                                  */
01070000/*   TEMPORARY DATA SET NAMES:                                      */
01080000/*                                                                  */
01090000/*     NONE.                                                        */
01100000/*                                                                  */
01110000/*------------------------------------------------------------------*/
01120000/*                                                                  */
01130000/*   COMMENTS:                                                      */
01140000/*                                                                  */
01150000/*     (1)    IF THERE ARE NON-PAIRED MEMBERS IN YOUR PHASE, YOU    */
01160000/*            MUST MANUALLY DELETE THE NON-PAIRED MEMBERS FROM THE  */
01170000/*            MEMBER LIST.                                          */
01180000/*                                                                  */
01190000/*     (2)    A 'SUPERC RETURN CODE = 0' MEANS THAT NO DIFFERENCES  */
01200000/*            WERE FOUND. A 'SUPERC RETURN CODE = 1' MEANS THAT     */
01210000/*            DIFFERENCES WERE FOUND.                               */
01220000/*                                                                  */
01230000/*------------------------------------------------------------------*/
01240000/*                                                                  */
01250000/*   REVISION HISTORY:                                              */
01260000/*                                                                  */
01270000/*     DATE      PCR#  NAME  DESCRIPTION                            */
01280000/*     --------  ----  ----  -------------------------------------  */
01290000/*     08/05/91  0202  TEV   ORIGINAL CLIST                         */
01300000/*                                                                  */
01310000/*------------------------------------------------------------------*/
01320000/********************************************************************/
01330000PROC 1 PASS                                          +
01340000       NEWFILE(NCAMCM.BFS.CURRENT.&PASS..PROCS)      +
01350000       OLDFILE(&SYSUID..REL99V0.&PASS..PROCS)        +
01360000       MRGEFILE(&SYSUID..REL99V0.&PASS..MERGE.PROCS) +
01370000       LISTFILE(&SYSUID..REL99V0.&PASS..COMPARE)     +
01380000       UPDATEDK(&SYSUID..REL99V0.&PASS..UPDATE)      +
01390000       PROFILE(&SYSUID..SUPERC.STMTS)                +
01400000       LIST()                                        +
01410000       MERGE                                         +
01420000       TRACE
01430000 
01440000GLOBAL RETCODE
01450000 
01460000IF &TRACE. NE  THEN +
01470000      CONTROL LIST CONLIST
01480000 
01490000CONTROL END(DONE)
01500000 
01510000      /***********************************/
01520000      /*    ON ATTN, DO THE FOLLOWING    */
01530000      /***********************************/
01540000ATTN    DO
01550000     CLOSFILE MEMLIST
01560000     FREE F(MEMLIST)
01570000  EXIT
01580000DONE
01590000 
01600000      /*************************************/
01610000      /*    CHECK IF THE NEWFILE EXISTS    */
01620000      /*************************************/
01630000 
01640000IF  &SYSDSN('&NEWFILE.') NE  OK THEN DO
01650000    WRITE &NEWFILE. DOES NOT EXIST!
01660000    EXIT
01670000DONE
01680000 
01690000      /*************************************/
01700000      /*    CHECK IF THE OLDFILE EXISTS    */
01710000      /*************************************/
01720000 
01730000IF  &SYSDSN('&OLDFILE.') NE  OK THEN DO
01740000    WRITE &OLDFILE. DOES NOT EXIST!
01750000    EXIT
01760000DONE
01770000 
01780000      /**************************************************************/
01790000      /*    IF THE MERGE PROCS LIST FILE DOES NOT EXIST, CREATE IT. */
01800000      /**************************************************************/
01810000 
01820000IF  &SYSDSN('&MRGEFILE.') NE OK THEN +
01830000    ALLOC DA('&MRGEFILE.') SPACE (65 10) BLKSIZE(6160) LRECL(88) +
01840000          RECFM(F B) DSORG(PO) DIR(25) TRACKS NEW
01850000 
01860000      /*****************************************************/
01870000      /*    IF THE LIST FILE DOES NOT EXIST, CREATE IT.    */
01880000      /*****************************************************/
01890000 
01900000IF  &SYSDSN('&LISTFILE.') NE OK THEN +
01910000    ALLOC DA('&LISTFILE.') SPACE (40 10) BLKSIZE(3325) LRECL(133) +
01920000          RECFM(F B A) DSORG(PO) DIR(25) TRACKS NEW
01930000 
01940000      /*************************************************************/
01950000      /*    IF THE UPDATE DECKS FILE DOES NOT EXIST, CREATE IT     */
01960000      /*************************************************************/
01970000 
01980000IF  &SYSDSN('&UPDATEDK.') NE OK THEN +
01990000    ALLOC DA('&UPDATEDK.') SPACE (40 10) BLKSIZE(1600) LRECL(80) +
02000000          RECFM(F B) DSORG(PO) DIR(25) TRACKS NEW
02010000 
02020000      /*****************************/
02030000      /*    MAKE MEMBER LISTING    */
02040000      /*****************************/
02050000 
02060000IF &LIST. EQ  THEN DO
02070000   PDSALL &OLDFILE. LIB(&SYSUID..XPLUPDAT) MAKELIST
02080000   SET &SAVECC = &LASTCC
02090000   SET &LIST = &SYSUID..XPLUPDAT.MEMLIST
02100000DONE
02110000 
02120000IF &SAVECC GT 0 THEN DO
02130000  WRITE ERROR IN CREATING A MEMBER LIST...EXITING PROGRAM
02140000  EXIT
02150000DONE
02160000 
02170000IF &MERGE = MERGE THEN GOTO MERGE
02180000DO
02190000 
02200000      /***************************************************/
02210000      /*    CREATE THE COMPARES LIST AND UPDATE DECKS    */
02220000      /***************************************************/
02230000 
02240000  ALLOC F(MEMLIST)  DA('&LIST.')  SHR
02250000 
02260000  OPENFILE   MEMLIST   INPUT
02270000 
02280000  GETFILE  MEMLIST
02290000 
02300000  DO WHILE &LASTCC. NE 400
02310000 
02320000     SET &MEMLEN = 1
02330000     DO WHILE &SUBSTR(&MEMLEN.:&MEMLEN.,&MEMLIST.) NE
02340000        SET &MEMLEN = &MEMLEN + 1
02350000     DONE
02360000 
02370000     SET &MEMLEN  = &MEMLEN - 1
02380000     SET &MEMNAME = &SUBSTR(1:&MEMLEN.,&MEMLIST.)
02390000 
02400000      /************************************************/
02410000      /*    NOW THAT YOU HAVE MEMBER NAME, CREATE     */
02420000      /*    UPDATE DECK VIA SUPERC PROGRAM.           */
02430000      /************************************************/
02440000 
02450000     WRITE
02460000     WRITE EXECUTING SUPERC ON MEMBER &MEMNAME..
02470000 
02480000     SUPERC  &MEMNAME.                               +
02490000             NEWFILE(&NEWFILE.(&MEMNAME.))           +
02500000             OLDFILE(&OLDFILE.(&MEMNAME.))           +
02510000             OUTDD(&LISTFILE.(&MEMNAME.))            +
02520000             DELDD(&UPDATEDK.(&MEMNAME.))            +
02530000             STMTFILE(&PROFILE.) PROCESS('UPDSEQ0,REFMOVR')
02540000 
02550000     ALLOC F(CHANNEL4) DA('&OLDFILE.(&MEMNAME.)') SHR
02560000     ALLOC F(CHANNEL5) DA('&UPDATEDK.(&MEMNAME.)') SHR
02570000     ALLOC F(CHANNEL6) DA('&MRGEFILE.(&MEMNAME.)') SHR
02580000     ALLOC F(CHANNEL7) DA (*)
02590000 
02600000     WRITE
02610000     WRITENR MERGE OF MEMBER &MEMNAME....
02620000 
02630000     CALL 'NCAMCM.TOOLS.HALLOAD(XPLUPDAT)' 'MSGLEVEL=0'
02640000     SET &SAVECC = &LASTCC
02650000 
02660000     IF &SAVECC = 0 THEN +
02670000        WRITE SUCCESSFUL.
02680000     ELSE +
02690000        WRITE ERROR CODE &SAVECC..
02700000 
02710000     FREE F(CHANNEL4, CHANNEL5, CHANNEL6)
02720000 
02730000     GETFILE MEMLIST
02740000 
02750000  DONE
02760000  CLOSFILE MEMLIST
02770000  FREE F(MEMLIST)
02780000DONE
02790000MERGE: DO
02800000 
02810000      /*********************************/
02820000      /*    MERGE THE TWO COMPILERS    */
02830000      /*********************************/
02840000 
02850000  ALLOC F(MEMLIST)  DA('&LIST.')  SHR
02860000 
02870000  OPENFILE MEMLIST INPUT
02880000 
02890000  GETFILE MEMLIST
02900000 
02910000  DO WHILE &LASTCC. NE 400
02920000 
02930000     SET &MEMLEN = 1
02940000     DO WHILE &SUBSTR(&MEMLEN.:&MEMLEN.,&MEMLIST.) NE
02950000        SET &MEMLEN = &MEMLEN + 1
02960000     DONE
02970000 
02980000     SET &MEMLEN  = &MEMLEN - 1
02990000     SET &MEMNAME = &SUBSTR(1:&MEMLEN.,&MEMLIST.)
03000000 
03010000     ALLOC F(CHANNEL4) DA('&OLDFILE.(&MEMNAME.)') SHR
03020000     ALLOC F(CHANNEL5) DA('&UPDATEDK.(&MEMNAME.)') SHR
03030000     ALLOC F(CHANNEL6) DA('&MRGEFILE.(&MEMNAME.)') SHR
03040000     ALLOC F(CHANNEL7) DA (*)
03050000 
03060000     WRITE
03070000     WRITENR MERGE OF MEMBER &MEMNAME....
03080000 
03090000     CALL 'NCAMCM.TOOLS.HALLOAD(XPLUPDAT)' 'MSGLEVEL=0'
03100000     SET &SAVECC = &LASTCC
03110000 
03120000     IF &SAVECC = 0 THEN +
03130000        WRITE SUCCESSFUL.
03140000     ELSE +
03150000        WRITE ERROR CODE &SAVECC..
03160000 
03170000     FREE F(CHANNEL4, CHANNEL5, CHANNEL6)
03180000 
03190000     GETFILE MEMLIST
03200000  DONE
03210000 
03220000  CLOSFILE MEMLIST
03230000  FREE F(MEMLIST)
03240000DONE
