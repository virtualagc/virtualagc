### FILE="Main.annotation"
## Copyright:	Public domain.
## Filename:	IMU_PERFORMANCE_TESTS_1.agc
## Purpose:	A section of Sunrise 69.
##		It is part of the reconstructed source code for the final
##		release of the Block I Command Module system test software. No
##		original listings of this program are available; instead, this
##		file was created via disassembly of dumps of Sunrise core rope
##		memory modules and comparison with the later Block I program
##		Solarium 55.
## Assembler:	yaYUL --block1
## Contact:	Ron Burkey <info@sandroid.org>.
## Website:	www.ibiblio.org/apollo/index.html
## Mod history:	2023-06-19 MAS	Created from Solarium 55.
## 		2023-06-21 MAS	Updated for Sunrise 69, which involved copious
##				swapping of code with IMU PERFORMANCE TESTS 2.


		SETLOC	54000
		
ALGNTST		TC	GRABDSP		# SXT-NB-IMU FINE ALIGNMENT TEST
		TC	PREGBSY
		
		CAF	LABLAT1
		TS	YSM2
		CAF	LABLAT1 +1
		TS	YSM2 +1

		CAF	ZERO
		TS	TIME2
		TS	NDXCTR
		TS	TESTTIME
		TS	EROPTN

		TC	DSPYLOAD

		XCH	TAR1POS
		TS	LATITUDE
		XCH	TAR1POS +1
		TS	LATITUDE +1

		CAF	72DEC
		TS	MAXPTS2

		TC	INTPRET
		VMOVE	0
			TARGET1
		STORE	TAR1POS

		VMOVE	0
			TARGET2
		STORE	TAR2POS

		VMOVE	0
			TARGET3
		STORE	TAR3POS

		VMOVE	0
			TARGET4
		STORE	TAR4POS

		ITC	0
			ALGNINIT

		EXIT	0

		CS	ZERO
		TS	CDUIND

		TC	BANKCALL
		CADR	IMUZERO
		
		
POSLOAD		CAF	V24N30E		# R1 POSITION 1,2,3
		TC	NVSUB
		TC	PRENVBSY
		TC	ENDIDLE
		TC	ENDTEST
		TC	-5
		XCH	DSPTEM1
		TS	POSITON
		INDEX	DSPTEM1 +1
		TC	+1
		TC	POSLOAD1
		TC	POSLOAD2
		TC	POSLOAD3
		TC	POSLOAD4

POSLOAD2	CAF	ZERO
		TC	POSLOAD3 +1

POSLOAD4	CAF	ZERO
		TS	NDXCTR
		AD	12DEC
		TC	+3

POSLOAD3	CAF	12DEC
		TS	NDXCTR
		AD	TEN
		TS	TESTTIME
		TC	DSPYLOAD

POSLOAD1	CAF	ONE
		TS	COAROFIN
		TC	FINDNAVB	# COARSE ALIGN MARKS
		TC	BANKCALL
		CADR	IMUSTALL	# WAIT FOR IMUZERO COMPLETION
		TC	ENDTEST

POSNJUMP	INDEX	POSITON
		TC	+1
		TC	ENDTEST
		TC	POS1
		TC	POS2
		TC	POS3
		
POSNRETN	TC	PUTPOSX
		
		TC	OGCZERO
		
		TC	BANKCALL
		CADR	IMUFINE		# INITIATE FINE ALIGN MODE
		TC	BANKCALL
		CADR	IMUSTALL	# WAIT FOR IMUFINE COMPLETION
		TC	ENDTEST
		
		CAF	ZERO
		TS	COAROFIN
		TC	FINDNAVB	# FINE ALIGN MARKS
		TC	SMDCALC		# FINE ALIGN TORQUING
		TC	BANKCALL
		CADR	IMUSTALL	# WAIT FOR PULSING COMPLETION
		TC	ENDTEST

ERFINAL		TC	INTPRET
		ITC	0
			EARTHR
		ITC	0
			OUTGYR
		EXIT	0
		TC	BANKCALL
		CADR	IMUSTALL
		TC	ENDTEST

ERRETN		CCS	EROPTN
		TC	ERFINAL
		INHINT
		TC	TPTIME
		RELINT
		CAF	ZERO
		TS	PIPAX
		TS	PIPAY
		TS	PIPAZ
		TS	STOREPL
		TS	NDXCTR		# UPDATE STORAGE LOCATION
		TC	STORRSLT
		TS	NDXCTR
		INHINT
		CAF	60SEC
		TC	WAITLIST
		CADR	PIP1
		RELINT
		CAF	PIP2ADR
		TC	JOBSLEEP
		
PIP1		CAF	PIP2ADR
		TC	JOBWAKE
		TC	TASKOVER
		
PIP2		CS	PIPNDX
		TS	PIPINDX2
		TC	BANKCALL
		CADR	CHECKG
		RELINT
		TC	STORRSLT
		TS	NDXCTR
		CS	PIPNDX +1
		TS	PIPINDX2
		TC	BANKCALL
		CADR	CHECKG
		RELINT
		TC	STORRSLT
		TS	NDXCTR
		INHINT
		CAF	30SEC
		TC	WAITLIST
		CADR	PIP1
		RELINT
		CAF	PIP2ADR
		TC	JOBSLEEP

GOMISALN	TC	MISALIGN
PIP2ADR		CADR	PIP2

FINDNAVB	XCH	Q
		TS	QPLACE
		TC	BANKCALL
		CADR	MKRELEAS
		CAF	ONE
		TS	DSPTEM1
		CAF	V01N30E		# MARK ON TARGET 1
		TC	NVSUB
		TC	PRENVBSY
		CAF	ONE
		TC	BANKCALL
		CADR	SXTMARK
		TC	BANKCALL
		CADR	OPTSTALL
		TC	ENDTEST
		INDEX	MARKSTAT
		CS	0
		CS	A
		TS	VACADR		# TIME OF MARK
		INDEX	MARKSTAT
		CS	1
		CS	A
		TS	VACADR +1
		CS	MARKSTAT
		TS	MKSTAT1
		TC	BANKCALL
		CADR	MKRELEAS
		CAF	FOUR
		TS	DSPTEM1
		CAF	V01N30E		# MARK ON TARGET 4
		TC	NVSUB
		TC	PRENVBSY
		CAF	ONE
		TC	BANKCALL
		CADR	SXTMARK
		CCS	COAROFIN	# COARSE ALIGN OR FINE ALIGN MARKS
		TC	CLGNMARK
		CS	MKSTAT1
		AD	TWO
		INDEX	FIXLOC
		TS	S1		# BASE ADDRESS GIMBAL ANGLES
		TC	INTPRET		# OPT-NB-SM    TARGET 1
		LXA,1	0
			MKSTAT1
		ITC	0
			SXTNB
		VSLT	1
		ITC
			STARM
			0
			NBSM
		NOLOD	0
		STORE	STARAD
		DMOVE	0
			VACADR
		STORE	TMARK
		EXIT	0
		
EARRTCOM	TC	INTPRET		# COMPENSATE BETWEEN FINE ALIGN MARKS
		ITC	0
			EARTHR
		ITC	0
			OUTGYR
		EXIT	0
		TC	BANKCALL
		CADR	IMUSTALL
		TC	ENDTEST
		CCS	OPTCADR
		TC	+3
		TC	EARRTCOM
		TC	+1
		TC	BANKCALL
		CADR	OPTSTALL
		TC	ENDTEST
		CAF	TWO
		AD	MARKSTAT
		INDEX	FIXLOC
		TS	S1		# BASE ADDRESS GIMBAL ANGLES
		TC	INTPRET		# OPT-NB-SM    TARGET 4
		LXC,1	0
			MARKSTAT
		ITC	0
			SXTNB
		VSLT	1
		ITC
			STARM
			0
			NBSM
		NOLOD	0
		STORE	STARAD +6
		ITC	0
			MATXDET
		
CLGNMARK	TC	INTPRET		# OPT-NB       TARGET 1
		LXA,1	0
			MKSTAT1
		ITC	0
			SXTNB
		VMOVE	0
			STARM
		STORE	STARAD
		EXIT	0
		TC	BANKCALL
		CADR	OPTSTALL
		TC	ENDTEST
		TC	INTPRET		# OPT-NB       TARGET 4
		LXC,1	0
			MARKSTAT
		ITC	0
			SXTNB
		VMOVE	0
			STARM
		STORE	STARAD +6
		
MATXDET		VMOVE	0		# CALCULATE TRANSFORMATION MATRIX
			TAR1POS
		STORE	6
		VMOVE	0
			TAR1POS +6
		STORE	12D
		ITC	0
			AXISGEN
		VMOVE	0
			XDC
		STORE	STARAD
		VMOVE	0
			YDC
		STORE	STARAD +6
		VMOVE	0
			ZDC
		STORE	STARAD +12D
		EXIT	0
		TC	QPLACE
		

PUTPOSX		XCH	Q
		TS	QPLACE
		TC	INTPRET
		ITC	0
			CALCGA
		EXIT	0

		CS	OGC
		COM
		TS	THETAD

		CS	IGC
		COM
		TS	THETAD +1

		CS	MGC
		COM
		TS	THETAD +2

		CAF	ZERO
		TS	KH
		CAF	KGIMU
		TS	KG

		TC	BANKCALL
		CADR	IMUCOARS	# INITIATE COARSE ALIGN MODE
		TC	BANKCALL
		CADR	IMUSTALL	# WAIT FOR IMUCOARS COMPLETION
		TC	ENDTEST
		TC	QPLACE



SMDCALC		XCH	Q
		TS	QPLACE
		TC	INTPRET
		MXV	1
		VSLT
			XSM
			STARAD
			1
		STORE	XDC
		MXV	1
		VSLT
			YSM
			STARAD
			1
		STORE	YDC
		VXV	1
		VSLT
			XDC
			YDC
			1
		STORE	ZDC
		ITC	0
			CALCGTA
		ITC	0
			OUTGYR
		EXIT	0
		TC	QPLACE


ALGNINIT	RTB	0
			ZEROVAC
		DMOVE	0
			ZERODP
		COS	1
		COMP
			LATITUDE
		SIN	1
		VDEF	VXSC		# (SIN,-COS,0)
			LATITUDE
			OMEG/MS
		STORE	VMARK
		RTB	0
			LOADTIME
		STORE	TMARK
		ITCQ	0


EARTHR		RTB	0
			LOADTIME
		STORE	S1
		NOLOD	3
		DSU	TSLT
		VXSC	MXV
		VAD
			TMARK
			10D
			VMARK
			XSM
			OGC
		STORE	OGC
		DMOVE	0
			S1
		STORE	TMARK
		ITCQ	0
		

OUTGYR		AXT,1	1
		AST,1	AXT,2
			6
			2
			0
OUTGYR1		TSRT*	0
			OGC +6,1
			8D,2
		STORE	GYROD +6,1
		TSLT*	1
		BDSU*
			GYROD +6,1
			8D,2
			OGC +6,1
		STORE	OGC +6,1
		TIX,1	1
		RTB	ITCQ
			OUTGYR1
			PULSEIMU

OGCZERO		XCH	Q		# ZERO RESIDUAL TORQUING
		TS	QPLACE
		CAF	FIVE
		TS	OVCTR
		CAF	ZERO
		INDEX	OVCTR
		TS	OGC
		CCS	OVCTR
		TC	OGCZERO +3
		TC	QPLACE


TPTIME		XCH	Q
		TS	QPLAC
		CS	TIME2
		CS	A
		TS	MPAC
		CS	TIME1
		CS	A
		TS	MPAC +1
		CS	IN2
		CS	IN2
		CS	A
		MASK	FINEMSK2
		TS	MPAC +2
		CCS	A
		TC	+7
		CS	TIME2
		CS	A
		TS	MPAC
		CS	TIME1
		CS	A
		TS	MPAC +1
		XCH	MPAC +2
		EXTEND
		MP	BIT11
		XCH	LP
		TS	MPAC +2
		TC	QPLAC


DSPYLOAD	XCH	Q
		TS	QPLACE
		TC	+2
		TS	NDXCTR
		INDEX	NDXCTR
		CS	YSM2
		COM
		TS	DSPTEM1
		INDEX	NDXCTR
		CS	YSM2 +1
		COM
		TS	DSPTEM1 +1
		CS	NDXCTR
		COM
		AD	YSM2CADR
		TS	DSPTEM1 +2
		TC	BANKCALL
		CADR	FLASHON
		CAF	V05N30E
		TC	NVSUB
		TC	PRENVBSY
		CAF	V07N30E
		TC	NVSUB
		TC	PRENVBSY
		TC	ENDIDLE		# WAIT FOR DATA OR PROCEED
		TC	ENDTEST
		TC	+2
		TC	DSPYLOAD +4	# RE-DISPLAY IF DATA LOADED
		CS	NDXCTR
		AD	TESTTIME
		CCS	A
		CAF	TWO
		AD	NDXCTR
		TC	DSPYLOAD +3
		TC	QPLACE
YSM2CADR	CADR	YSM2


STORRSLT	XCH	Q
		TS	QPLACE
		TC	INTPRET
		LXC,1	0
			NDXCTR
		NOLOD	2
		TSLT	DMP
		RTB
			8D
			SCALFTR
			SGNAGREE
		STORE	GENPL,1
		EXIT	0
		XCH	STOREPL
		INDEX	NDXCTR
		TS	GENPL +2
		CS	NDXCTR
		AD	MAXPTS2
		CCS	A
		CAF	THREE
		AD	NDXCTR
		TC	QPLACE
		TC	GOMISALN


ENDTEST		TC	FREEDSP
		CS	FOUR
		MASK	MODREG
		TS	MODREG
		TC	BANKCALL
		CADR	DSPMM
		CS	ZERO
		TS	CDUIND
		TC	BANKCALL
		CADR	MKRELEAS
		TC	ENDOFJOB
		

POS1		TC	INTPRET
		VMOVE	0
			TAR3POS
		STORE	YSM
		VMOVE	0
			TAR4POS
		STORE	ZSM
		VXV	1
		UNIT
			YSM
			ZSM
		STORE	XSM
		EXIT	0
		CS	ONE
		TS	PIPNDX
		CS	TWO
		TS	PIPNDX +1
		TC	POSNRETN
		
		
		
POS2		TC	INTPRET
		VMOVE	0
			TAR3POS
		STORE	YSM
		VMOVE	0
			TAR4POS
		STORE	XSM
		VXV	1
		UNIT
			XSM
			YSM
		STORE	ZSM
		EXIT	0
		CS	ZERO
		TS	PIPNDX
		CS	ONE
		TS	PIPNDX +1
		TC	POSNRETN
		
		
		
POS3		TC	INTPRET
		VMOVE	0
			TAR3POS
		STORE	ZSM
		VMOVE	0
			TAR4POS
		STORE	XSM
		VXV	1
		UNIT
			ZSM
			XSM
		STORE	YSM
		EXIT	0
		CS	ZERO
		TS	PIPNDX
		CS	TWO
		TS	PIPNDX +1
		TC	POSNRETN


TARGET1		2DEC	0
		2DEC	.5
		2DEC	0

TARGET2		2DEC	0
		2DEC	.35355328
		2DEC	.35355328

TARGET3		2DEC	0
		2DEC	.35355328
		2DEC	-.35355328

TARGET4		2DEC	0
		2DEC	.35355328
		2DEC	.35355328

INTCON1		DEC	40 B+8
		DEC	0
KGIMU		DEC	.18
12DEC		DEC	12
72DEC		DEC	72
30SEC		DEC	3000
60SEC		DEC	6000
50DEC		DEC	50
SCALFTR		2DEC	.64
FINEMSK2	OCT	17
OMEG/MS		2DEC	.12169524
LABLAT1		2DEC	.11767824

V01N30E		OCT	0130
V24N30E		OCT	02430
V07N30E		OCT	00730
V05N30E		OCT	00530
V06N66E		OCT	00666

VERTRECT	NOLOD	1
		VXM	VSLT
			XSM
			1
		STORE	0
		DMOVE	0
			VAC +2
		COMP	0
			VAC +4
		SMOVE	1
		VDEF
			ZERODP
		NOLOD	1
		VXSC	VSLT
			INTCON1
			1
		VSU	3
		BOV	VXSC
		VSLT	VAD
		MXV	VAD
			0
			18D
			ERECTND
			INTCON1
			1
			-
			XSM
			OGC
		STORE	OGC
		VMOVE	0
		STORE	18D
ERECTND		ITCQ	0


POSGMBL		TC	BANKCALL
		CADR	IMUFINE
		TC	BANKCALL
		CADR	IMUSTALL
		TC	ENDTEST
		CS	CDUX
		CS	A
		INDEX	FLNDX
		TS	6
		CS	CDUY
		CS	A
		INDEX	FLNDX
		TS	2
		CS	CDUZ
		CS	A
		INDEX	FLNDX
		TS	4

FALNE1		CAF	TWO
		AD	FLNDX
		INDEX	FIXLOC
		TS	S1
		TC	INTPRET
		MXV	1
		VSLT	ITC
			XSM
			STARAD
			1
			NBSM
		NOLOD	0
		STORE	XDC
		MXV	1
		VSLT	ITC
			YSM
			STARAD
			1
			NBSM
		NOLOD	0
		STORE	YDC
		VXV	1
		VSLT
			XDC
			YDC
			1
		STORE	ZDC
		ITC	0
			CALCGTA
		ITC	0
			OUTGYR
		EXIT	0
		TC	BANKCALL
		CADR	IMUSTALL
		TC	ENDTEST
		TC	BANKCALL
		CADR	FALNED


MISALIGN	TC	OGCZERO
		CAF	11DEC
		TS	PTS
		CAF	ZERO
		TS	PTS +1
BBB		TS	NDXCTR
		INDEX	NDXCTR
		CS	GENPL +68D
		INDEX	NDXCTR
		AD	GENPL +74D
		INDEX	NDXCTR
		TS	GENPL +74D
		CAF	63DEC
		AD	NDXCTR
		CCS	A
		CS	THREE
		AD	NDXCTR
		TC	BBB
CCCC		TS	NDXCTR
		TC	INTPRET
		LXA,1	0
			NDXCTR
		DMOVE*	1
		DSU
			GENPL +72D,1
			GENPL
		STORE	GENPL +72D,1
		EXIT	0
		CS	NDXCTR
		AD	69DEC
		CCS	A
		CAF	THREE
		AD	NDXCTR
		TC	CCCC
		TS	GENPL
		TS	GENPL +1
		TS	RUN
		CAF	THREE
DDDD		TS	NDXCTR
		TC	INTPRET
		LXA,1	0
			NDXCTR
		DSU*	0
			GENPL +72D,1
			GENPL +66D,1
		NOLOD	1
		TSRT	DAD*
			1
			GENPL +66D,1
		STORE	GENPL +72D,1
		NOLOD	1
		TSLT	DAD
			2
			OGC
		STORE	OGC
		DMOVE*	2
		TSLT	DSQ
		DAD
			GENPL +72D,1
			4
			MGC
		STORE	MGC
		EXIT	0
		CS	RUN
		COM
		AD	NDXCTR
		COM
		AD	63DEC
		CCS	A
		CAF	SIX
		AD	NDXCTR
		TC	DDDD
		AD	RUN
EEEE		TS	NDXCTR
		INDEX	NDXCTR
		CS	GENPL +11D
		COM
		TS	TEMDELV
		CAF	ZERO
		TS	TEMDELV +1
		TC	INTPRET
		LXC,1	0
			NDXCTR
		DMOVE	0
			-
		NOLOD	2
		TSLT	BDDV
		DAD
			5
			TEMDELV
			IGC
		STORE	IGC
		TSLT	1
		BDDV
			-
			3
			TEMDELV
		DMOVE*	2
		TSLT	DMP
		DAD
			GENPL +9D,1
			4
			-
			GENPL
		STORE	GENPL
		EXIT	0
		CS	NDXCTR
		AD	63DEC
		AD	RUN
		AD	NEG3
		CCS	A
		CAF	SIX
		AD	NDXCTR
		TC	EEEE
		TC	INTPRET
		DMP	0
			OGC
			GENPL
		DMP	1
		DSU
			MGC
			IGC
		DSQ	1
		TSRT
			OGC
			1
		TSLT	2
		DMP	DSU
		BDDV	DMP
			PTS
			11
			MGC
			-
			-
			K
		STORE	DSPTEM2
		EXIT	0
		TC	BANKCALL
		CADR	FLASHON
		CAF	V06N66E
		TC	NVSUB
		TC	PRENVBSY
		TC	ENDIDLE
		TC	ENDTEST
		CAF	THREE
		TS	RUN
		TC	OGCZERO
		CAF	ZERO
		TS	GENPL
		TS	GENPL +1
		TC	DDDD



K		2DEC	1.233 E3 B-14
11DEC		DEC	11
63DEC		DEC	63
69DEC		DEC	69
FLNDX		ADRES	GENPL


GYROTORK	TC	GRABDSP
		TC	PREGBSY
		CAF	V21N30E
		TC	NVSUB
		TC	PRENVBSY
		TC	ENDIDLE
		TC	ENDOFJOB
		TC	+1
		XCH	DSPTEM1
		TS	TESTNO
		TS	CALCDIR
		CAF	QUARTER
		TS	AZIMUTH
		TS	GENPL +76D
		CAF	SIX
		TS	NBPOS
		CCS	TESTNO
		TS	TESTNO
		TC	+5
		TS	TESTNO
		CS	BURSTPOS
		TS	SAVE
		TC	+3
		CAF	BURSTPOS
		TS	SAVE
		CS	TESTNO
		AD	TWO
		TS	NEGCDU2
		CCS	A
		TC	BANKCALL
		CADR	STEVEIN1
		TC	+1
		TC	BANKCALL
		CADR	STEVEIN3
		
		
		
TORK		CS	NEGCDU2
		COM
		CCS	A
		INDEX	TESTNO
		CS	CDUX
		TC	+2
		CS	CDUX
		TS	NEGCDU1
		CS	TESTNO
		COM
		AD	TESTNO
		TS	LOCNO
		CAF	ITERNO
BLOGS		TS	BUBBLE
		INHINT
		CAF	TIMEINCR
		TC	WAITLIST
		CADR	WAKEUP
		RELINT
		CAF	FIVE
		TS	PLOW
		CAF	ZERO
		INDEX	PLOW
		TS	GYROD
		CCS	PLOW
		TC	-5
		CS	SAVE
		COM
		INDEX	LOCNO
		TS	GYROD +1
		TC	BANKCALL
		CADR	IMUPULSE
		TC	BANKCALL
		CADR	IMUSTALL
		NOOP
		CAF	WAKECADR
		TC	JOBSLEEP
WAKEUP		CAF	WAKECADR
		TC	JOBWAKE
		TC	TASKOVER
WAKE		CCS	BUBBLE
		TC	BLOGS
		CS	NEGCDU2
		COM
		CCS	A
		INDEX	TESTNO
		CS	CDUX
		TC	+2
		CS	CDUX
		TS	NEGCDU2
		CCS	CALCDIR		# DONE TO INSURE PROPER SIGN
		CS	NEGCDU1
		TC	+4
		CS	NEGCDU2
		TS	NEGCDU2
		TC	+2
		TS	NEGCDU1
		INHINT
		CS	NEGCDU2
		XCH	A
		TS	ITEMP2
		CS	NEGCDU1
		TC	BANKCALL
		CADR	2SCOMDIF
		RELINT
		TS	CUSSANG
		EXTEND
		MP	FIVE
		XCH	LP
		TS	DSPTEM1
		TC	BANKCALL
		CADR	FLASHON
		CAF	V07N30E
		TC	NVSUB
		TC	PRENVBSY
		TC	ENDIDLE
		TC	ENDOFJOB


BURSTPOS	DEC	256
V21N30E		OCT	02130
ITERNO		DEC	4095
TIMEINCR	DEC	24
WAKECADR	CADR	WAKE
PRIO24		OCT	24000


SYSTEST		CAF	FIVE
		MASK	MODREG
		CCS	A
		TC	TOOBUSY
		XCH	Q
		TS	QPLACE
		CAF	FOUR
		AD	MODREG
		TS	MODREG
		TC	BANKCALL
		CADR	DSPMM
		TC	QPLACE

TOOBUSY		TC	FALTON
		TC	ENDOFJOB


## MAS 2023: The following functions are the entry points for the IMU performance
## tests added to Sunrise 69. Since the three modules of Sunrise 45 were not
## modified in the creation of Sunrise 69, these functions are not referred to
## anwhere and must be invoked manually, by address, using verb 20 (request
## executive). Modern comments below give the procedure needed to start each.

## The gyro drift test is started via:
##   V21N26E 04000E
##   V20N01E 55675E
GOGYDRFT	TC	SYSTEST		# GYRO DRIFT TEST.
		INHINT
		CAF	PRIO24
		TC	FINDVAC
		CADR	GYDRFT
		TC	ENDOFJOB

## The PIPA scale factor determination test is started via:
##   V21N26E 04000E
##   V20N01E 55703E
GOACCEL		TC	SYSTEST		# PIPA SCALE FACTOR DETERMINATION.
		INHINT
		CAF	PRIO24
		TC	FINDVAC
		CADR	ACCELTST
		TC	ENDOFJOB

## The IMU check test is started via:
##   V21N26E 04000E
##   V20N01E 55711E
GOIMUCHK	TC	SYSTEST		# IMU CHECK.
		INHINT
		CAF	PRIO24
		TC	FINDVAC
		CADR	IMUCHK
		TC	ENDOFJOB

## The IMU alignment test is started via:
##   V21N26E 04000E
##   V20N01E 55717E
GOALGN		TC	SYSTEST		# IMU ALIGNMENT TEST.
		INHINT
		CAF	PRIO24
		TC	FINDVAC
		CADR	ALGNTST
		TC	ENDOFJOB

## The gyro torquing test is started via:
##   V21N26E 04000E
##   V20N01E 55725E
GOGYTORK	TC	SYSTEST		# GYRO TORQUING TEST.
		INHINT
		CAF	PRIO24
		TC	FINDVAC
		CADR	GYROTORK
		TC	ENDOFJOB
