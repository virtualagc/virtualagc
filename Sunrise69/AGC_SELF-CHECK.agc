### FILE="Main.annotation"
## Copyright:	Public domain.
## Filename:	AGC_SELF-CHECK.agc
## Purpose:	A section of Sunrise 69.
##		It is part of the reconstructed source code for the final
##		release of the Block I Command Module system test software. No
##		original listings of this program are available; instead, this
##		file was created via disassembly of dumps of Sunrise core rope
##		memory modules and comparison with the later Block I program
##		Solarium 55.
## Assembler:	yaYUL --block1
## Contact:	Ron Burkey <info@sandroid.org>.
## Website:	www.ibiblio.org/apollo/index.html
## Mod history:	2023-06-19 MAS	Created from Sunrise 45.


		SETLOC	ENDEXTVS

SBIT1		OCTAL	00001
SBIT2		OCTAL	00002
SBIT3		OCTAL	00004
SBIT4		OCTAL	00010
SBIT5		OCTAL	00020
SBIT6		OCTAL	00040
SBIT7		OCTAL	00100
SBIT8		OCTAL	00200
SBIT9		OCTAL	00400
SBIT10		OCTAL	01000
SBIT11		OCTAL	02000
SBIT12		OCTAL	04000
SBIT13		OCTAL	10000
SBIT14		OCTAL	20000
SBIT15		OCTAL	40000

# THE FOLLOWING CONSTANTS ARE USED THROUGHOUT SELF-CHECK
SCON0		OCTAL	+0
SCON1		OCTAL	+1
SCON2		OCTAL	+2
SCON4		OCTAL	+4
SCON64		OCTAL	00100
SCON1/4		OCTAL	10000
SCON3/8		OCTAL	14000
SCON1/2		OCTAL	20000
SCONTS		OCTAL	37775
SCONSU		OCTAL	37776
SCON+MAX	OCTAL	37777
SCON-3/8	OCTAL	63777
SCON-1/2	OCTAL	67777
SCON-2		OCTAL	77775
SCON-1		OCTAL	77776
SCONMAX		OCTAL	77777

ERRORS		XCH	Q		# FAILURE DETECTED - ALARM.
		TS	SFAIL		# SAVE CALLING Q FOR POSSIBLE FAILURE LOC.
		TC	ALARM
		OCT	01102
		CAF	ZERO
		TS	SMODE
		TC	DUMMYJOB
		
# CHECKS MOST CCS PULSES
CCSCHK		TS	SMODE
		CS	SCON2		# -2
		CCS	A		# C(A) = -2
		TC	ERRORS
		TC	ERRORS
		TC	+2
		TC	ERRORS
		CCS	A		# C(A) = +1, RESULT OF CCS -NUMBER
		TC	+4
		TC	ERRORS
		TC	ERRORS
		TC	ERRORS
		CCS	A		# C(A) = +0, RESULT OF CCS + NUMBER
		TC	ERRORS
		TC	+3
		TC	ERRORS
		TC	ERRORS
		CS	A
		CCS	A		# C(A) = -0, RESULT OF CCS +0
		TC	ERRORS
		TC	ERRORS
		TC	ERRORS
		CCS	A		# RESULT OF CCS -0
		TC	ERRORS
		TC	+3
		TC	ERRORS
		TC	ERRORS
# SPECIFICALLY CHECKS RSC PULSE OF TC INSTRUCTION (ALSO MOST OF TC
# PULSES)
		TC	+2
		TC	+2		# NEXT SUBROUTINE
		TC	Q

		AD	SBIT1
		TS	OKREG

# CHECKS WP, GP, TP - WP2, RP2 - RG, WP, OF CCS1
# CHECKS RB, WG PULSES (READ BACK INTO ERASABLE)
PTY+ERAS	CAF	5777		# 47777
		TS	SKEEP1
		MASK	SKEEP1
		XCH	SKEEP1
		AD	SKEEP1
		INDEX	5777
		4	SKEEP1		# MP SKEEP1
		INDEX	5777
		5	SKEEP1		# DV SKEEP1
		CS	SKEEP1
		TS	SKEEP2		# 30000
		INDEX	SKEEP1		# TROUBLE IF C(SKEEP1) NOT 47777
		6	SKEEP2		# SU SKEEP2, C(A) = -0
		TS	SKEEP2		# -0
		CCS	SKEEP2
		TC	ERRORS
		TC	ERRORS
		TC	ERRORS
		CCS	SKEEP2
		TC	ERRORS
		TC	ERRORS
		TC	ERRORS		
		CS	OKREG
		CS	A
		AD	SBIT2
		TS	OKREG
# START ERASABLE INSTRUCTION CHECK
		CAF	EINST1
		TS	SKEEP1
		CAF	EINST2
		TS	SKEEP2
		CAF	EINST3
		TS	SKEEP3
		CAF	EINST4
		TS	SKEEP4
		CAF	EINST5
		TS	SKEEP5
		CAF	EINST6
		TS	SKEEP6
		CAF	SCON1/2
		TC	SKEEP1
EINST1		INDEX	5777
EINST2		4	SCON2		# MP, C(A) = +1, THEN +0
EINST3		AD	A		# CHECKS ST2 PARITY
EINST4		CCS	A
EINST5		TC	SKEEP1
EINST6		TC	+1		# NEXT SUBROUTINE

		CS	OKREG
		CS	A
		AD	SBIT3
		TS	OKREG

# CHECKS RSC, WSC PULSES
# NO WSC PULSE IN MASK INSTRUCTION
SCCHK		CAF	SCON64		# 00100
		XCH	LP		# 00040
		XCH	LP
		TS	LP		# 00020
		AD	LP		# 00010
		INDEX	LP		# 00004
		2	5767		# INDEX 5777
		6	0003		# SU LP, 00002
# NEXT 4 INSTRUCTIONS CHECK RSC PULSE IN MASK INSTRUCTION
		CS	LP		# C(A) = -2, C(LP) = +1
		MASK	LP		# C(A) = +1, C(LP) = +1
		AD	SCON-1
		CCS	A
		TC	ERRORS
		TC	ERRORS
		TC	ERRORS
		CCS	LP
		TC	+4
		TC	ERRORS
		TC	ERRORS
		TC	ERRORS
		CCS	LP
		TC	ERRORS
		TC	ERRORS
		TC	+2
		TC	ERRORS
		CCS	LP
		TC	ERRORS
		TC	+3		# NEXT SUBROUTINE
		TC	ERRORS
		TC	ERRORS
		CS	OKREG
		CS	A
		AD	SBIT4
		TS	OKREG
# CHECKS MOST OF MP PULSES
MPCHK		CAF	SCON4
		TS	LP
MP++		CAF	SCON+MAX
		INDEX	5777
		4	LP		# C(A) = +1, CHECKS RSC PULSE
		AD	LP		# C(LP) = +37776
		TS	SKEEP1		# 37777
MP+-		CAF	SCON+MAX
		INDEX	5777
		4	SCON-2
		AD	LP		# C(LP) = -37776
		AD	SKEEP1
		CCS	A
		TC	ERRORS
		TC	ERRORS
		TC	ERRORS
MP--		CS	SCON+MAX
		INDEX	5777
		4	SCON-2		# C(A) = +1
		AD	LP		# C(LP) = +37776
		TS	SKEEP1		# 37777
MP-+		CS	SCON+MAX
		INDEX	5777
		4	SCON2		# C(A) = -1
		AD	LP		# C(LP) = -37776
		AD	SKEEP1
		CCS	A
		TC	ERRORS
		TC	ERRORS
		TC	ERRORS

		CS	OKREG
		CS	A
		AD	SBIT5
		TS	OKREG

# CHECKS MOST OF SU PULSES
SUCHK		CAF	SCON+MAX
		INDEX	5777
		6	SCONSU		# SU 37776, C(A) = +1
		CS	A
		CCS	A
		TC	ERRORS
		TC	ERRORS
		CCS	A
		TC	ERRORS

		CS	OKREG
		CS	A
		AD	SBIT6
		TS	OKREG

# CHECKS MOST OF DV PULSES (ALL EXCEPT WP, GP, TP)
# DIVIDE USES ST2
# ++ AND --, C(A) = 25252, C(Q) = 67777, C(LP) = +1
# +- AND -+ , C(A) = 52525, C(Q) = 67777, C(LP) = 40000 AND 40001
DVCHK		CAF	SCON3/8
		TS	Q
DV++		CAF	SCON1/4
		INDEX	5777
		5	Q		# C(A) = 25252, CHECKS RSC PULSE
		TS	SKEEP1
		XCH	LP
		TS	SKEEP2
DV+-		CS	Q		# +1/4
		INDEX	5777
		5	SCON-3/8	# C(A) = 52525
		AD	SKEEP1		# C(A) = -0
		CCS	A
		TC	ERRORS
		TC	ERRORS
		TC	ERRORS
		XCH	LP
		TS	SKEEP3
DV--		XCH	Q		# -1/4
		INDEX	5777
		5	SCON-3/8	# C(A) = 25252
		TS	SKEEP1
		XCH	LP
		TS	SKEEP4
DV-+		XCH	Q		# -1/4
		INDEX	5777
		5	SCON3/8
		AD	SKEEP1		# C(A) = -0
		TS	SKEEP1
		CS	LP		# C(A) = 37776
		AD	SKEEP1		# C(A) = 37776
		AD	SKEEP2		# C(A) = 37777
		AD	SKEEP3		# C(A) = -0
		AD	SKEEP4		# C(A) = +1
		CS	A
		CCS	A
		TC	ERRORS
		TC	ERRORS
		CCS	A
		TC	ERRORS

		CS	OKREG
		CS	A
		AD	SBIT7
		TS	OKREG

# CHECKS MOST TS PULSES
# CHECKS ALL OF PINC AND MINC PULSES EXCEPT WOVR
TS+-CHK		CAF	SCON1
		TS	OVCTR
		AD	SCON+MAX	# C(A) = + WITH OVERFLOW
		TS	SKEEP1
		TC	ERRORS
		AD	SKEEP1		# C(A) = +1
		AD	OVCTR		# C(A) = +3
		TS	OVCTR
		CS	A		# C(A) = 77774
		INDEX	5777
		6	SCONTS		# C(A) =  -0 WITH UNDERFLOW
		TS	SKEEP1
		TC	ERRORS
		AD	SKEEP1		# C(A) = -1
		AD	OVCTR		# C(A) = -1+2
		CS	A
		CCS	A
		TC	ERRORS
		TC	ERRORS
		CCS	A
		TC	ERRORS

		CS	OKREG
		CS	A
		AD	SBIT8
		TS	OKREG

		XCH	SCOUNT +1
		AD	ONE
		TS	SCOUNT +1
		TC	SMODECHK
		AD	SCOUNT
		XCH	SCOUNT

# COMPUTER ACTIVITY LIGHT (GREEN LIGHT) MAINTENANCE.

SMODECHK	CCS	NEWJOB		# SEE IF ITS TIME FOR A CHANGE.
		TC	DUMEXIT
		
ADVAN		CCS	SMODE		# SEE IF SELF-CHECK IS WANTED
		TC	CCSCHK		# YES PULSES ONLY
		TC	SMODECHK
		TC	CCSCHK +1	# YES PULSES + SC + ERASABLE
		TC	SMODECHK
		
DUMEXIT		CS	TWO		# TURN ON GREEN LIGHT (COMP ACT) AND
		INHINT
		MASK	OUT1		# GO TO CHANG1.
		AD	TWO
		TS	OUT1
		TC	CHANG1
		
DUMMYJOB	CS	TWO		# IDLING AGAIN- TURN OFF ACTIVITY LIGHT
		INHINT
		MASK	OUT1		# LIGHT.
		TS	OUT1
		RELINT
		TC	SMODECHK	# BACK TO CHECKING COMPUTER

ENDSELFC	EQUALS
